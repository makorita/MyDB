/*
ファイル内の<file:〇〇>で指定したファイル名〇〇で、そこから下のテキストを
ファイル保存する。
*/

//実行中ファイルのハンドル取得
#handle = hidemaruhandle( 0 );

disabledraw;
disableinvert;

gofiletop;

$curFilename="";
while(1){
	searchdown2 "<file:.*?>", regular;
	if((!result) && strlen($curFilename)==0){
		//message "0_"+$curFilename;
		break;
		
	}else if(result && strlen($curFilename)==0){
		//message "1_"+$curFilename;
		//ヒットしたファイル名を取得
		$searchStr=gettext(foundtopx,foundtopy,foundendx,foundendy);
		call s_replace,$searchStr,"<file:","";
		$searchStr=$$return;
		call s_replace,$searchStr,">","";
		$nextFilename=$$return;
		
		//次回対応
		down 1;
		golinetop;
		#bx=x;
		#by=y;
		$curFilename=$nextFilename;
		
	}else if(result && strlen($curFilename)>0){
		//message "2_"+$curFilename;
		//ヒットしたファイル名を取得
		$searchStr=gettext(foundtopx,foundtopy,foundendx,foundendy);
		call s_replace,$searchStr,"<file:","";
		$searchStr=$$return;
		call s_replace,$searchStr,">","";
		$nextFilename=$$return;
		
		//ファイル作成
		#ex=x;
		#ey=y;
		call SaveFile,$curFilename;
		
		//次回対応
		down 1;
		golinetop;
		#bx=x;
		#by=y;
		$curFilename=$nextFilename;
		
	}else if((!result) && strlen($curFilename)>0){
		//message "3_"+$curFilename;
		//ファイル作成
		gofileend;
		#ex=x;
		#ey=y;
		call SaveFile,$curFilename;
		break;
	}
}

endmacro;



//関数
SaveFile:
	$$filename=$$1;
	
	$$tmpStr = gettext(#bx,#by,#ex,#ey);
	openfile "/h";
	insert $$tmpStr;
	saveas $$filename;
	#stealth_handle=hidemaruhandle(0);
	setactivehidemaru #handle;
	closehidemaruforced #stealth_handle;
	return;

s_replace:

	// 引数を各変数に代入
	$$str = $$1;
	$$str_pre = $$2;
	$$str_after = $$3;
	$$reg = $$4;
	
	// 実行中のウィンドウハンドルを取得（メインファイル）
	##h_main = hidemaruhandle(0);

	// 新規作成状態の見えない秀丸エディタが起動（ステルスファイル）
	openfile "/h";

	//  実行中のウィンドウハンドルを取得（ステルスファイル）
	##h_sub = hidemaruhandle(0);

	// insert
	insert $$str;
	
	// 置換
	if ($$reg == "regular") {
		// 正規表現
		replaceallfast $$str_pre, $$str_after, regular;
	} else {
		// 通常
		replaceallfast $$str_pre, $$str_after;
	}
	
	// すべてを選択
	selectall;
	
	// 選択範囲の文字列を取得（範囲選択を維持しない）
	$$str_re = gettext(seltopx,seltopy,selendx,selendy);
	
	// メインファイルをアクティブにする
	setactivehidemaru ##h_main;

	// ステルスファイルを閉じる
	closehidemaruforced ##h_sub;
	
	// 戻る
	return $$str_re;
